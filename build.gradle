plugins {
   id 'net.nemerosa.versioning' version '2.6.1'
}
apply plugin: 'base'


group = 'com.logicartisan.intrepid'


versioning {
   dirty = { t -> t }
}
version = versioning.info.display
if ( !"RELEASE".equalsIgnoreCase( "$System.env.BUILD" ) ) {
	version = version + "-SNAPSHOT"
}
println "Version: ${version}"
println "Full version info: ${versioning.info}"


subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 11

	group = project.rootProject.group
	version = project.rootProject.version

	ext.sanitizedName = project.name.replace( '/', '-' )
	archivesBaseName = "intrepid-${sanitizedName}"


    repositories {
        mavenCentral()
    }



	test {
		systemProperty 'intrepid.lease.duration', '2000'
		systemProperty 'intrepid.lease.prune_interval', '1000'
		systemProperty 'intrepid.local_call_handler.initial_reservation', '10000'
		systemProperty 'intrepid.req_invoke_ack_rate_sec', '1'

		ignoreFailures true
	}



    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        baseName = "intrepid-${sanitizedName}"
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        baseName = "intrepid-${sanitizedName}"
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }


    signing {
        sign configurations.archives
    }


    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    tasks.withType(Javadoc).all { enabled = false }     // opt-in

    jar {
        println "Group: ${project.group}   Name: ${project.name}   Sanitized: ${sanitizedName}"
        baseName = "intrepid-${sanitizedName}"
    }



    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
	                artifactId "intrepid-${sanitizedName}"

                    packaging 'jar'
                    url 'https://bitbucket.org/robeden/intrepid'

					description 'A replacement for Java RMI (Remote Method Invocation) that allows for simpler and more transparent usage with more flexible control over session and security.'

                    scm {
                        connection 'scm:git:https://bitbucket.org/robeden/intrepid.git'
                        developerConnection 'scm:git:https://bitbucket.org/robeden/intrepid.git'
                        url 'https://bitbucket.org/robeden/intrepid/src'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'robeden'
                            name 'Rob Eden'
                            email 'robeden1@gmail.com'
                        }
                    }
                }

				pom.withXml {
					asNode().appendNode('name', "Intrepid/${sanitizedName}")
				}
            }
        }
    }
    uploadArchives.dependsOn sourcesJar
    uploadArchives.dependsOn javadocJar
}